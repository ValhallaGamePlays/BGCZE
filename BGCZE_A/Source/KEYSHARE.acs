// A script that shares keys with everyone because they're awesome.
// By Zhs2.

#library "FEATURESPILLOWWILLNOTSHUTUPABOUT.txt"
#include "zcommon.acs"

#define MAX_PLAYERS 8
#define MAX_KEY_TYPES 20

str keydb[MAX_KEY_TYPES][2] = {
// Class name | Nice name
// Doom
{ "BlueCard2",	"\c[Blue]Blue Cardkey\c-"	},
{ "BlueSkull2",	"\c[LightBlue]Blue Skullkey\c-"},
{ "RedCard2",	"\c[Red]Red Cardkey\c-"		},
{ "RedSkull2",	"\c[Brick]Red Skullkey\c-"	},
{ "YellowCard2","\c[Yellow]Yellow Cardkey\c-"},
{ "YellowSkull2","\c[Gold]Yellow Skullkey\c-"},
// Heretic
{ "KeyBlue",	"\c[Blue]Blue Gemkey\c-"	},
{ "KeyGreen",	"\c[Green]Green Gemkey\c-"	},
{ "KeyYellow",	"\c[Yellow]Yellow Gemkey\c-"},
// Hexen
{ "KeyAxe",		"\c[Brown]Axe Key\c-"		},
{ "KeyCastle",	"\c[Gold]Castle Key\c-"		},
{ "KeyCave",	"\c[DarkBrown]Cave Key\c-"	},
{ "KeyDungeon",	"\c[Grey]Dungeon Key\c-"	},
{ "KeyEmerald",	"\c[Green]Emerald Key\c-"	},
{ "KeyFire",	"\c[Red]Fire Key\c-"		},
{ "KeyHorn",	"\c[Olive]Horn Key\c-"		},
{ "KeyRusted",	"\c[Tan]Rusted Key\c-"		},
{ "KeySilver",	"\c[White]Silver Key\c-"	},
{ "KeySteel",	"\c[DarkGrey]Steel Key\c-"	},
{ "KeySwamp",	"\c[DarkGreen]Swamp Key\c-"	},
};

int keysfound[MAX_KEY_TYPES];

script "Key Sharer" OPEN
{
	int firstrun;
	
	if(!GameType() == GAME_NET_COOPERATIVE)
	{
		// It won't become a coop game so let's just
		terminate;
	}
	
	if(!firstrun)
	{
		delay(35*3); // Wait a few seconds for everything to get started
		++firstrun; // I sure hope players don't immediately find keys anyway
	}
	
	if(GetCVar("GC_KeyShare"))
	{
		for(int i = 0; i < MAX_KEY_TYPES; i++) // Key possession check.
		{
			for(int j = 0; j < MAX_PLAYERS; j++) // Swapping players.
			{
				if(!SetActivator(0, AAPTR_PLAYER1<<j))
				{
					continue; // Can't set our player so keep going
				}
				
				if(CheckInventory(keydb[i][0]) && !keysfound[i])
				{
					SetActivator(0, AAPTR_PLAYER1<<j);
					SetFont("SMALLFNT");
					HUDMessageBold(n:0, s:"\c- acquired the ", s:keydb[i][1], s:"\c-.";HUDMSG_FADEOUT|HUDMSG_LOG, 900, CR_UNTRANSLATED, 0.5, 0.1, 3.0, 0.5);
					for(int k = 0; k < MAX_PLAYERS; k++)
					{
						SetActivator(0, AAPTR_PLAYER1<<k);
						if(!CheckInventory(keydb[i][0]))
						{
							GiveInventory(keydb[i][0], 1);
						}
					}
					keysfound[i] = 1;
				}
			}
		}
	}
	
	SetActivator(0,AAPTR_NULL); // Probably would be bad if a player took this script with them when they left the game.
	delay(1);
	restart;
}

script "Key Equalizer" ENTER // Give the gift of key this season!
{
	if(GetCVar("GC_KeyShare"))
	{
		for(int i = 0; i < MAX_KEY_TYPES; i++)
		{
			if(keysfound[i])
			{
				GiveInventory(keydb[i][0], 1);
			}
		}
	}
}