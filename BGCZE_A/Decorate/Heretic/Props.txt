ACTOR GizmoBlue2 : KeyGizmoFloatBlue replaces KeyGizmoFloatBlue
{
  //+FLOATBOB
  Renderstyle Add
  States
  {
  Spawn:
    KGZB A random(2,3) Bright A_SpawnItemEx("BlueGizmoFade",frandom(4.0,-4.0),frandom(4.0,-4.0),frandom(-6.0,-2.0),frandom(0.2,0.6),frandom(0.2,0.6),frandom(0.6,1.4),frandom(0.0,360.0),SXF_CLIENTSIDE)
    Loop
  }
}

ACTOR BlueGizmoFade
{
	+NOINTERACTION
	+NOGRAVITY
	Renderstyle Add
	Scale 1
	Alpha 0
	States
	{
	Spawn:
	KGZB A 0
	Main:
	"####" A 0 A_ChangeVelocity(0,0,frandom(0.0,0.25),CVF_RELATIVE)
	"####" A 1 A_SetScale(ScaleX+0.05)
	"####" A 0 A_FadeIn(frandom(0.05,0.1))
	"####" A 0 A_JumpIf(Alpha >= 0.5, "PhaseTwo")
	Loop
	PhaseTwo:
	"####" A 0 A_ChangeVelocity(0,0,frandom(0.0,0.25),CVF_RELATIVE)
	"####" A 1 A_SetScale(ScaleX-0.15)
	"####" A 0 A_FadeOut(frandom(0.05,0.1))
	"####" A 0 A_JumpIf(Alpha <= 0.0, "NULL")
	Loop
	}
}

ACTOR GizmoGreen2 : KeyGizmoFloatGreen replaces KeyGizmoFloatGreen
{
  Renderstyle Add
  States
  {
  Spawn:
    KGZG A random(2,3) Bright A_SpawnItemEx("GreenGizmoFade",frandom(4.0,-4.0),frandom(4.0,-4.0),frandom(-6.0,-2.0),frandom(0.2,0.6),frandom(0.2,0.6),frandom(0.6,1.4),frandom(0.0,360.0),SXF_CLIENTSIDE)
    Loop
  }
}

ACTOR GreenGizmoFade : BlueGizmoFade
{
	States
	{
	Spawn:
	KGZG A 0
	KGZG A 0 A_Jump(256,"Main")
	Stop
	}
}

ACTOR GizmoYellow2 : KeyGizmoFloatYellow replaces KeyGizmoFloatYellow
{
  Renderstyle Add
  States
  {
  Spawn:
    KGZY A random(2,3) Bright A_SpawnItemEx("YellowGizmoFade",frandom(4.0,-4.0),frandom(4.0,-4.0),frandom(-6.0,-2.0),frandom(0.2,0.6),frandom(0.2,0.6),frandom(0.6,1.4),frandom(0.0,360.0),SXF_CLIENTSIDE)
    Loop
  }
}

ACTOR YellowGizmoFade : BlueGizmoFade
{
	States
	{
	Spawn:
	KGZY A 0
	KGZY A 0 A_Jump(256,"Main")
	Stop
	}
}