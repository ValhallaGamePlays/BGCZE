ACTOR Juggernaut : Hectebus Replaces Hectebus
{
	//Loot
	DropItem Diamond
	DropItem StratSpawner 32
	
	//Mecha Lardass can destroyed with many ways...
	// but it'll take a long time, though...
	//It is still impervious to melee attacks and ice, being so fat.
	//The regular pound attack however is shockingly good on it.
	//Just make sure it doesn't make you its floormat.

	//Resist & Weaknesses
	DamageFactor Fire, 1.2
	DamageFactor Meteorfist, 1.2
	DamageFactor Acid, 1.25
	DamageFactor Pound, 3.0
	DamageFactor Manuel, 1.25
	DamageFactor Crow, 1.2
	DamageFactor Crow2, 1.2
	DamageFactor Matriarch, 0.5
	
	DamageFactor Ice, 0.7
	
	DamageFactor CloseCombat, 0.6
	DamageFactor Shoelaces, 0.6
	DamageFactor SuperCloseCombat, 0.6
	DamageFactor SuperShoelaces, 0.6
	DamageFactor SuperMidasCloseCombat, 0.6
	DamageFactor SuperMidasShoelaces, 0.6
	
	//PAAAAAIN
	PainChance 10
	PainChance Crow, 1
	PainChance Crow2, 1
	
	//Guaranteed Effects
	PainChance SuperCloseCombat, 256
	PainChance SuperShoelaces, 256
	PainChance SuperMidasCloseCombat, 256
	PainChance SuperMidasShoelaces, 256
	PainChance Acid, 256
	PainChance Meteorfist, 256
	
	//Chance Effects
	PainChance Lightning, 144
	PainChance LightningBlue, 16
	PainChance Manuel, 120
	PainChance Fire, 80
	
	var int User_DeathByFire;
	var int User_DeathByTiberium;
	var int User_DeathByBlueTiberium;
	var int User_LightningLock;
	var int User_Smoke;
	-NOICEDEATH
	States
	{
	/////////////////////
	Spawn:
		BS1W A 1
	    TNT1 A 0 A_SpawnItemEx ("KillTreads",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		Goto Standby
	Standby:
	    BS1W A 1
	    BS1W AC 10 A_Look
	    Loop
	//////////////////
	See:
	    BS1W A 1
		//TNT1 A 0 A_SpawnItem("KillTreads")
	Goto Rolling
	Rolling:
	    
		TNT1 A 0 A_PlaySound("juggernaut/engine")
		
		BS1W A 2 A_Chase("","")
		BS1W B 2 A_Recoil(-2)
		TNT1 A 0 A_SpawnItem("TreadsDamage",65)
		BS1W A 2 A_Recoil(-2)
		BS1W B 2 A_Recoil(-2)
		TNT1 A 0 A_SpawnItem("TreadsDamage",65)
		
		BS1W C 2 A_Recoil(-2)
		BS1W D 2 A_Recoil(-2)
		TNT1 A 0 A_SpawnItem("TreadsDamage",65)
		BS1W C 2 A_Recoil(-2)
		BS1W D 2 A_Recoil(-2)
		TNT1 A 0 A_SpawnItem("TreadsDamage",65)
		
		BS1W A 2 A_Chase("","")
		BS1W B 2 A_Recoil(-2)
		TNT1 A 0 A_SpawnItem("TreadsDamage",65)
		BS1W A 2 A_Recoil(-2)
		BS1W B 2 A_Recoil(-2)
		TNT1 A 0 A_SpawnItem("TreadsDamage",65)
		
		BS1W C 2 A_Recoil(-2)
		BS1W D 2 A_Recoil(-2)
		TNT1 A 0 A_SpawnItem("TreadsDamage",65)
		BS1W C 2 A_Recoil(-2)
		BS1W D 2 A_Recoil(-2)
		TNT1 A 0 A_Jump (128, "Rolling2")
		TNT1 A 0 A_SpawnItem("TreadsDamage",65)
		Loop
		
	Rolling2:
	    
		TNT1 A 0 A_PlaySound("juggernaut/engine")
		
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Chase("","")
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Recoil(-2)
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Recoil(-2)
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Recoil(-2)
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Chase("","")
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Recoil(-2)
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Recoil(-2)
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Recoil(-2)
		TNT1 A 0 A_CheckSight("See")
		Goto Missile
		
	///////////////////	
	
	Missile:
	    TNT1 A 0
		TNT1 A 0 A_JumpIfCloser(150, "Rolling")
		TNT1 A 0 A_Jump (128, "FireRockets")
        Goto FireMachinegun
	FireMachinegun:
	    TNT1 A 0
		TNT1 A 0 A_PlaySound("juggernaut/engine")
		
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Chase("","")
		
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Recoil(-2)
		TNT1 A 0 A_CustomMissile ("JuggernautBody2", 0)
		TNT1 A 0 A_CustomMissile("MonsterMinigunTracer", 72, 42, random(-8, 8), 0)
		TNT1 A 0 A_CustomMissile("MonsterMinigunTracer", 72, -42, random(-8, 8), 0)
		TNT1 A 0 A_CustomMissile("YellowFlareSmall", 72, 42, 0, 0)
		TNT1 A 0 A_CustomMissile("YellowFlareSmall", 72, -42, 0, 0)
		TNT1 A 0 A_CustomMissile("RifleCaseSpawn", 72, 42, 0, 0)
		TNT1 A 0 A_CustomMissile("RifleCaseSpawn", 72, -42, 180, 0)
		TNT1 A 0 A_SpawnItem("TreadsDamage",65)
		BS1B A 2 A_Recoil(-2)
		
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Recoil(-2)
		
		TNT1 A 0 A_CustomMissile ("JuggernautBody2", 0)
		TNT1 A 0 A_CustomMissile("MonsterMinigunTracer", 72, 42, random(-8, 8), 0)
		TNT1 A 0 A_CustomMissile("MonsterMinigunTracer", 72, -42, random(-8, 8), 0)
		TNT1 A 0 A_CustomMissile("YellowFlareSmall", 72, 42, 0, 0)
		TNT1 A 0 A_CustomMissile("YellowFlareSmall", 72, -42, 0, 0)
		TNT1 A 0 A_CustomMissile("RifleCaseSpawn", 72, 42, 0, 0)
		TNT1 A 0 A_CustomMissile("RifleCaseSpawn", 72, -42, 180, 0)
		TNT1 A 0 A_SpawnItem("TreadsDamage",65)
		TNT1 A 0 A_GiveInventory("JuggernautMachinegunAmmo", 1)
		TNT1 A 0 A_JumpIfInventory("JuggernautMachinegunAmmo", 30, "stopfiring")
		TNT1 A 0 A_CheckSight("StopFiring")
		Loop
		
	FireRockets:
	    TNT1 A 0
		TNT1 A 0 A_PlaySound("juggernaut/engine")
		
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Chase("","")
		
		TNT1 A 0 A_CustomMissile("Rocket", 110, -42, random(-3, 3), 0)
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Recoil(-2)
		
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		TNT1 A 0 A_SpawnItem("TreadsDamage",65)
		BS1B A 2 A_Recoil(-2)
		
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Recoil(-2)
		
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Chase("","")
		TNT1 A 0 A_CustomMissile("Rocket", 110, 42, random(-3, 3), 0)
		
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Recoil(-2)
		
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		TNT1 A 0 A_SpawnItem("TreadsDamage",65)
		BS1B A 2 A_Recoil(-2)
		
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		BS1B A 2 A_Recoil(-2)
		
		TNT1 A 0 A_CustomMissile ("JuggernautBody1", 0)
		
		
		TNT1 A 0 A_SpawnItem("TreadsDamage",65)
		TNT1 A 0 A_GiveInventory("JuggernautRocketAmmo", 2)
		TNT1 A 0 A_JumpIfInventory("JuggernautRocketAmmo", 6, "stopfiring")
		TNT1 A 0 A_CheckSight("StopFiring")
		Loop	
    
	StopFiring:
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("JuggernautMachinegunAmmo", 30)
		TNT1 A 0 A_TakeInventory("JuggernautRocketAmmo", 6)
		Goto See
		
	
	
	
	////////////////////
	Pain:	
	    TNT1 A 0
	    BS1W A 2
		BS1W A 2 A_Pain
		Goto Rolling
		
		
	///////////////////////
	Death:
		TNT1 A 0 A_StopSound(5)
		"####" "######" 0 A_DropItem("MajorManaPod",-1,224)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItem("KillTreads")
		EXPL A 0 Radius_Quake (6, 100, 0, 22, 0)
        TNT1 AAAA 0 A_CustomMissile ("ExplosionSpawner", 90, 0, random (0, 360), 2, random (0, 180))
        TNT1 AAAAAA 0 A_CustomMissile ("ExplosionSpawner", 90, 0, random (0, 180), 2, random (0, 180))
        TNT1 AAAA 0 A_CustomMissile ("UltraGoreSPawner", 90, 0, random (0, 360), 2, random (0, 180))
        TNT1 AAAAAA 0 A_CustomMissile ("UltraGoreSpawner", 90, 0, random (0, 180), 2, random (0, 180))
        TNT1 A 0 A_CustomMissile ("CyberXDeath", 0, 0, random (0, 360), 2, random (0, 160))
		TNT1 A 0
		BS1B B 5 A_CustomMissile ("ExplosionSmoke", 60, 0, random (0, 360), 2, random (70, 110))
		BS1B B 5 A_CustomMissile ("RealFlameTrailsMedium", 60, 0, random (0, 360), 2, random (0, 160))
		BS1B B 5 A_CustomMissile ("ExplosionSmoke", 60, 0, random (0, 360), 2, random (70, 110))
		BS1B B 5 A_CustomMissile ("RealFlameTrailsMedium", 60, 0, random (0, 360), 2, random (0, 160))
		BS1B B 5 A_CustomMissile ("ExplosionSmoke", 60, 0, random (0, 360), 2, random (70, 110))
		BS1B B 5 A_CustomMissile ("RealFlameTrailsMedium", 60, 0, random (0, 360), 2, random (0, 160))
		BS1B B 5 A_CustomMissile ("ExplosionSmoke", 60, 0, random (0, 360), 2, random (70, 110))
		BS1B B 5 A_CustomMissile ("RealFlameTrailsMedium", 60, 0, random (0, 360), 2, random (0, 160))
		BS1B B 5 A_CustomMissile ("ExplosionSmoke", 60, 0, random (0, 360), 2, random (70, 110))
		BS1B B 5 A_CustomMissile ("RealFlameTrailsMedium", 60, 0, random (0, 360), 2, random (0, 160))
		BS1B B 5 A_CustomMissile ("ExplosionSmoke", 60, 0, random (0, 360), 2, random (70, 110))
		BS1B B 5 A_CustomMissile ("RealFlameTrailsMedium", 60, 0, random (0, 360), 2, random (0, 160))
		BS1B B 5 A_CustomMissile ("ExplosionSmoke", 60, 0, random (0, 360), 2, random (70, 110))
		BS1B B 5 A_CustomMissile ("RealFlameTrailsMedium", 60, 0, random (0, 360), 2, random (0, 160))
		BS1B B 5 A_CustomMissile ("ExplosionSmoke", 60, 0, random (0, 360), 2, random (70, 110))
		BS1B B 5 A_CustomMissile ("RealFlameTrailsMedium", 60, 0, random (0, 360), 2, random (0, 160))
		BS1B B 5 A_CustomMissile ("ExplosionSmoke", 60, 0, random (0, 360), 2, random (70, 110))
		BS1B B 5 A_CustomMissile ("RealFlameTrailsMedium", 60, 0, random (0, 360), 2, random (0, 160))
		BS1B B 5 A_CustomMissile ("ExplosionSmoke", 60, 0, random (0, 360), 2, random (70, 110))
		BS1B B 5 A_CustomMissile ("RealFlameTrailsMedium", 60, 0, random (0, 360), 2, random (0, 160))
		BS1B B 5 A_CustomMissile ("ExplosionSmoke", 60, 0, random (0, 360), 2, random (70, 110))
		BS1B B 5 A_CustomMissile ("RealFlameTrailsMedium", 60, 0, random (0, 360), 2, random (0, 160))
		Goto Looplet	
	Looplet:
		BS1B B 0 A_JumpIf(User_Smoke == 8, "Smoke") //Original tic delay...
		"####" "#" 0 A_SetUserVar("User_Smoke", User_Smoke + 1)
		"####" "#" 0 A_JumpIfInventory("CorpseTrigger2",1,"CorpseBlastB")
		"####" "#" 1 A_JumpIfInventory("CorpseTrigger",1,"CorpseBlastA")
		"####" "#" 0 A_JumpIf(User_DeathByFire == 1,"FireMelt")
		"####" "#" 0 A_JumpIf(User_DeathByTiberium == 1, "TiberiumMelt")
		"####" "#" 0 A_JumpIf(User_DeathByBlueTiberium == 1, "BlueTiberiumMelt")
		Loop
	Smoke:
		"####" "#" 0 A_CustomMissile ("ExplosionSmoke", 60, 0, random (0, 360), 2, random (70, 110))
		"####" "#" 0 A_SetUserVar("User_Smoke", 0)
		Goto Looplet
//Guncaster Stuff...
	//Melee
	Pain.SuperCloseCombat:
	Pain.SuperMidasCloseCombat:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_ChangeVelocity(frandom(-2.0,-5.0),0,frandom(0.5,1.0),CVF_RELATIVE)
		Goto Pain
	Pain.SuperShoelaces:
	Pain.SuperMidasShoelaces:
		TNT1 A 1 A_FaceTarget
		TNT1 A 0 A_ChangeVelocity(frandom(-5.0,-10.0),0,frandom(3.0,6.0),CVF_RELATIVE)
		Goto Pain
	Death.SuperCloseCombat:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_ChangeVelocity(frandom(-2.0,-5.0),0,frandom(0.5,1.0),CVF_RELATIVE)
		Goto Death
	Death.SuperShoelaces:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_ChangeVelocity(frandom(-5.0,-10.0),0,frandom(3.0,6.0),CVF_RELATIVE)
		Goto Death
	Death.MidasCloseCombat:
	Death.MidasShoelaces:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_NoBlocking
		"####" "######" 0 A_DropItem("MajorManaPod",-1,224)
		TNT1 A 0 A_Pain
		TNT1 A 0 A_PlaySound("Midas/Touched")
		TNT1 A 0 A_SpawnItemEx("Midas_Statue_Juggernaut",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERSCALE) //SXF_TRANSFERSPRITEFRAME|SXF_SETMASTER)
		TNT1 A 5 A_BossDeath
		Stop
	Death.SuperMidasCloseCombat:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_NoBlocking
		"####" "######" 0 A_DropItem("MajorManaPod",-1,224)
		TNT1 A 0 A_Pain
		TNT1 A 0 A_PlaySound("Midas/Touched")
		TNT1 A 0 A_SpawnItemEx("Midas_Statue_Juggernaut",0,0,0,frandom(-2.0,-5.0),0,frandom(0.5,1.0),0,SXF_TRANSFERPOINTERS|SXF_SETMASTER|SXF_TRANSFERSCALE)
		TNT1 A 0 A_BossDeath
		TNT1 A 10 A_KillChildren
		stop
	Death.SuperMidasShoelaces:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_NoBlocking
		"####" "######" 0 A_DropItem("MajorManaPod",-1,224)
		TNT1 A 0 A_Pain
		TNT1 A 0 A_PlaySound("Midas/Touched")
		TNT1 A 0 A_SpawnItemEx("Midas_Statue_Juggernaut",0,0,0,frandom(-5.0,-10.0),0,frandom(3.0,6.0),0,SXF_TRANSFERPOINTERS|SXF_SETMASTER|SXF_TRANSFERSCALE)
		TNT1 A 0 A_BossDeath
		TNT1 A 10 A_KillChildren
		Stop
		
	//General death stuff
	Crush:
		//TNT1 A 0 A_BossDeath
		TNT1 AAAAAAAAAA 0 A_CustomMissile ("BloodTr255", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_PlaySound("misc/xdeath4")
		XBIG EEEEE 1 A_CustomMissile ("CeilBloodLauncherLong", 0, 0, random (0, 360), 2, random (50, 130))
		TNT1 A 0 A_CustomMissile ("GiantBloodSpot", 4, 0, random (0, 360), 2, random (0, 160))
		XBIG E 0
		Goto Looplet
		
	//Corpseblast
	CorpseBlastA:
		TNT1 AA 0 A_SpawnItemEx("CyberdemonBlast",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
	CorpseBlastB:
		"####" "##" 0 A_SpawnItemEx("CyberdemonBlast",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		"####" "#" 0 A_TakeInventory("CorpseTrigger2",1)
		Goto Looplet
	CorpseBlastC:
		"####" "##" 0 A_SpawnItemEx("CyberdemonBlast",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		"####" "#" 0 A_TakeInventory("CorpseTrigger2",1)
		Goto Looplet2

	//Gumpop
	Pain.Acid:
		BS1W A 0 A_JumpIfInventory("PowerGumpopDamage",1,2)
		"####" "#" 0 A_SpawnItemEx("GumpopIndicator",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_SETTARGET|SXF_ORIGINATOR|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE)
		"####" "#" 0 A_SpawnItemEx("GumpopGlow",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_SETTARGET|SXF_ORIGINATOR)
		"####" "#" 0 A_Jump(10,"Pain")
		Goto See
	Death.Acid:
		BS1W A 1 A_ChangeFlag("SOLID",0)
		"####" "#" 1 A_Playsound("Gumpop/Expand",5)
		"####" "###################################" 1 A_SetScale(ScaleX+frandom(0.01,0.025))
		"####" "#" 1 A_Playsound("Gumpop/Pop",5)
		"####" "#" 0 A_StopSound(5)
		"####" "#" 0 A_JumpIfInTargetInventory("Mana",0,2)
		"####" "#" 0 A_DropItem("MajorManaPod",-1,54)
        TNT1 A 0 A_CustomMissile ("MeatDeath", 0, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BloodMistExtraBig", 30, 0, random (0, 360), 2, random (60, 90))
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("GC_XDeath2b", 20, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("GC_XDeath3b", 20, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAAAAAAAAA 0 A_CustomMissile ("MuchBlood2", 50, 0, random (0, 360), 2, random (0, 160))
        //TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_BossDeath
		TNT1 A 0 A_SpawnItem ("GrowingBloodPool")
		TNT1 AAAA 0 A_SpawnItemEx("FatsoGibbed",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		XBIG ABCDE 5
	Looplet2:
		"####" "#" 0 A_JumpIfInventory("CorpseTrigger2",1,"CorpseBlastB")
		"####" "#" 1 A_JumpIfInventory("CorpseTrigger",1,"CorpseBlastA")
		Loop
	//Dreadful
	Death.Tiberium:
		TNT1 A 0 A_SetUserVar("User_DeathByTiberium", 1)
		TNT1 A 0 ACS_NamedExecuteAlways("Toxic Avenger")
		TNT1 A 0 A_SpawnItemEx("TiberiumDeath",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_SETTARGET|SXF_ORIGINATOR)
		TNT1 A 0 A_GiveInventory("TibDeathCounter")
		Goto Death
	TiberiumMelt:
		"####" "#" 0 A_JumpIf(ScaleX > 2.025, "EndOfMelt")
		"####" "#" 0 ACS_NamedExecuteAlways("Toxic Avenger")
		"####" "#" 1 A_SetScale(ScaleX + frandom(0.002,0.003),ScaleY - frandom(0.003,0.004))
		Goto Looplet
	Death.BlueTiberium:
		TNT1 A 0 A_SetUserVar("User_DeathByBlueTiberium", 1)
		TNT1 A 0 ACS_NamedExecuteAlways("Blue Oyster Cult")
		TNT1 A 0 A_SpawnItemEx("BlueTiberiumDeath",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_SETTARGET|SXF_ORIGINATOR)
		TNT1 A 0 A_GiveInventory("BlueTibDeathCounter")
		Goto Death
	BlueTiberiumMelt:
		"####" "#" 0 A_JumpIf(ScaleX > 2.025, "EndOfMelt")
		"####" "#" 0 ACS_NamedExecuteAlways("Blue Oyster Cult")
		"####" "#" 1 A_SetScale(ScaleX + frandom(0.002,0.003),ScaleY - frandom(0.003,0.004))
		Goto Looplet
		
	//Threadcutter
	Pain.Manuel:
		"####" "#" 0 A_Jump(128,1)
		Goto Pain
		"####" "#" 0 A_GiveInventory("BleedItem")
		"####" "#" 0 A_SpawnItemEx("BleedIndicator",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_SETTARGET|SXF_ORIGINATOR)
		Goto Pain
		
	//Thunderstruck
	Pain.Lightning:
		"####" "#" 0 A_Jump(164,1)
		Goto Pain
		BS1W A 0 A_ChangeFlag("NOPAIN",1)
		"####" "#" 0 A_SetUserVar("User_LightningLock", User_LightningLock + 10)
	LightningLock:
		"####" "#" 0 A_SpawnItem("HeadshotTarget10", 24, 62, 0)
		"####" "#" 0 A_SetUserVar("User_LightningLock", User_LightningLock - 1)
		"####" "#" 0 A_Pain
		"####" "#" 0 A_PlaySound("Thunderstruck/Shocked",5,0.6,1)
		"####" "##" 0 ACS_NamedExecuteAlways("Shock me Plenty")
		"####" "###" 2 A_SpawnItemEx("MonsterShock",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERPOINTERS)
		"####" "##" 0 ACS_NamedExecuteAlways("Shock me Plenty")
		"####" "###" 2 A_SpawnItemEx("MonsterShock",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERPOINTERS)
		"####" "##" 0 ACS_NamedExecuteAlways("Shock me Plenty")
		"####" "###" 2 A_SpawnItemEx("MonsterShock",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERPOINTERS)
		"####" "#" 0 A_JumpIf(User_LightningLock >= 1, "LightningLock")
		"####" "#" 0 A_StopSound(5)
		"####" "#" 0 A_ChangeFlag("NOPAIN",0)
		Goto See
		
	//Cauterizer
	Pain.LightningBlue:
		"####" "#" 0 A_Jump(48,1)
		Goto Pain
		BS1W A 0 A_ChangeFlag("NOPAIN",1)
		"####" "#" 0 A_SetUserVar("User_LightningLock", User_LightningLock + 10)
	BlueLightningLock:
		"####" "#" 0 A_SpawnItem("HeadshotTarget10", 24, 62, 0)
		"####" "#" 0 A_SetUserVar("User_LightningLock", User_LightningLock - 1)
		"####" "#" 0 A_Pain
		"####" "#" 0 A_PlaySound("Thunderstruck/Shocked",5,0.6,1)
		"####" "##" 0 ACS_NamedExecuteAlways("Shock me Plenty Blue")
		"####" "###" 2 A_SpawnItemEx("CauterMonsterShock",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERPOINTERS)
		"####" "##" 0 ACS_NamedExecuteAlways("Shock me Plenty Blue")
		"####" "###" 2 A_SpawnItemEx("CauterMonsterShock",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERPOINTERS)
		"####" "##" 0 ACS_NamedExecuteAlways("Shock me Plenty Blue")
		"####" "###" 2 A_SpawnItemEx("CauterMonsterShock",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERPOINTERS)
		"####" "#" 0 A_JumpIf(User_LightningLock >= 1, "BlueLightningLock")
		"####" "#" 0 A_StopSound(5)
		"####" "#" 0 A_ChangeFlag("NOPAIN",0)
		Goto See
		
	//Fire
	Pain.Meteorfist:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_ChangeVelocity(frandom(-5.0,-10.0),0,frandom(3.0,6.0),CVF_RELATIVE)
		"####" "#" 0 A_Jump(256,2) //Guaranteed "On Fire!" effect, heeeh.
	Pain.Fire:
		"####" "#" 0 A_Jump(16,1)
		Goto Pain
		"####" "#" 0 A_JumpIfInventory("PowerOnFire",1,"Pain")
		"####" "#" 0 A_GiveInventory("OnFireItem")
		"####" "#" 0 A_SpawnItemEx("OnFireIndicator",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_SETTARGET|SXF_ORIGINATOR)
		Goto Pain
	Death.Meteorfist:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_ChangeVelocity(frandom(-5.0,-10.0),0,frandom(3.0,6.0),CVF_RELATIVE)
	//Death.Lightning:
	//Death.LightningBlue:
	Death.Fire:
		"####" "#" 0 A_SetUserVar("User_DeathByFire", 1)
		"####" "#" 0 ACS_NamedExecuteAlways("Extra Crispy")
		"####" "#" 0 A_SpawnItemEx("FlamingDeath",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_SETTARGET|SXF_ORIGINATOR)
		Goto Death
	FireMelt:
		"####" "#" 0 A_JumpIf(ScaleX > 2.025, "EndOfMelt")
		"####" "#" 0 ACS_NamedExecuteAlways("Extra Crispy")
		"####" "#" 1 A_SetScale(ScaleX + frandom(0.002,0.003),ScaleY - frandom(0.003,0.004))
		Goto Looplet
	EndOfMelt:
		"####" "#" 0 A_SetUserVar("User_DeathByTiberium", 0)
		"####" "#" 0 A_SetUserVar("User_DeathByFire", 0)
		Goto Looplet
		
	//Crows
	Death.Crow:
		TNT1 AAAAAA 0 A_SpawnItemEx("FlockOfCrows",random(45,-45),random(45,-45),0,0,0,0,0,SXF_TRANSFERPOINTERS)
		Goto Death
		
	//Scourgebreath
	Death.Breath:
		"####" "#" 0 A_JumpIfInTargetInventory("Mana",0,2)
		"####" "#" 0 A_DropItem("MajorManaPod",-1,54)
		"####" "#" 0 ACS_NamedExecuteAlways("Extra Crispy")
		"####" "#" 0 A_Scream
		//"####" "#" 0 A_BossDeath
		"####" "#" 0 A_NoBlocking
		"####" "#" 0 A_SpawnItemEx("BreathSoulburst",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERPOINTERS)
	DeathBreathLoop:
		"####" "#" 0 A_SpawnItemEx("BreathDisintegration",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		"####" "#" 0 A_JumpIf(ScaleX <= 0, "Null")
		"####" "#" 0 A_FadeOut(0.05)
		"####" "#" 1 A_SetScale(ScaleX - 0.03, ScaleY - 0.005)
		Loop
		
	//Ice
	GenericFreezeDeath:
	Death.Ice:
	Death.Freeze:
	Death.Frost:
	Death.DukeIce:
		"####" "#" 5 A_GenericFreezeDeath
		"####" "#" 0 A_JumpIfInTargetInventory("Mana",0,2)
		"####" "#" 0 A_DropItem("MajorManaPod",-1,54)
		"----" A 0 A_JumpIfInventory("ChillgraspTrigger",1,2)
		"----" A 0 A_Jump(256,2)
		"----" A 0 A_SpawnItemEx("ChillgraspBlast",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		"----" A 1 A_FreezeDeathChunks
		Wait		
	}
}

ACTOR Midas_Statue_Juggernaut : Midas_Statue
{
	Radius 48
	Height 88
	Health 400
	States
	{
	Spawn:
		BS1W A 1
		Goto Super::Cycle
	Death:
	Crash:
		TNT1 A 0 A_SetUserVar("User_GoldRange",40)
		TNT1 A 0 A_SetUserVar("User_GoldAmount",70)
		TNT1 A 0 A_SetUserVar("User_GoldCounter1",70)
		TNT1 A 0 A_SetUserVar("User_GoldCounter2",50)
		Goto Super::DeathInit
	MidasReward:
		TNT1 A 0 A_JumpIf(CallACS("MoneyShareCheck"),2)
		TNT1 A 0 A_GiveToTarget("Dosh",800)
		Stop
		TNT1 A 0 ACS_NamedExecuteAlways("MoneySharing",0,800)
		Stop
	}
}